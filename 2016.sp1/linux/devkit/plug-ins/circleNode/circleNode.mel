// Copyright 2014 Autodesk, Inc. All rights reserved. 
//
// Use of this software is subject to the terms of the Autodesk 
// license agreement provided at the time of installation or download, 
// or which otherwise accompanies this software in either electronic 
// or hard copy form.

//-
// ==========================================================================
//
// ==========================================================================
//+

//
//  Autodesk Script File
//
//  Creation Date:   30 September 1996
//
//  Description:
//     This script creates a new top level Maya menu that contains a
//	   single item "Move in Circle".  When selected, it will create
//     a sphere and a dependency node that moves this in a circle,
//     and connect these 2 together.
//
//     When the play button on the time slider is pressed, the sphere
//     will move in a cirle around the Y axis.
//
//  Procedures:
//     circleMenu, createSphereAndAttachCircleNode
//


//	Callback routine for the menu item created in CircleMenu
global proc createSphereAndAttachCircleNode ()
{
	// Create a circle node dependency object called "circleNode1"
	createNode circle -n circleNode1;

	// Create a sphere called "sphere1"
	sphere -n sphere1 -r 1;

	// Connect the sine output attribute of "circleNode1"
	// to the X position of "sphere1"
	connectAttr circleNode1.sineOutput sphere1.translateX;

	// Connect the cosine output attribute of "circleNode1"
	// to the Z position of "sphere1"
	connectAttr circleNode1.cosineOutput sphere1.translateZ;

	// Connect the output of the time slider, "time1", in frames
	// to the input of "circleNode1".
	connectAttr time1.outTime circleNode1.input;

	// "circleNode1" will now compute the X and Z positions of "sphere1"
	// as a function of the frame in the current animation.
}

global proc circleMenu ()
{

	// The global string variable gMainWindow contains the name of top
	// Level Maya window.  Using this as the parent in a menu command
	// will create a new menu at the same level as "File", "Edit", etc.
	global string $gMainWindow;

	// Create a top level menu called "Circle".  Its only menu item
	// is called "Move in circle", and when invoked by the user, it
	// will call the createSphereAndAttachCircleNode procedure shown above.
	menu -parent $gMainWindow -tearOff true -label "Circle" circleMenu;
		menuItem -label "Move in circle"
			-command "createSphereAndAttachCircleNode" circleMenuItem1;
}

// Run circleMenu to add "Circle" to the top level Maya menu list.
circleMenu;
