// Copyright 2014 Autodesk, Inc. All rights reserved. 
//
// Use of this software is subject to the terms of the Autodesk 
// license agreement provided at the time of installation or download, 
// or which otherwise accompanies this software in either electronic 
// or hard copy form.

//-
// ==========================================================================
//
// ==========================================================================
//+

// Usage:
// 
// 	Use jitterNode.mel to insert jitter DG node between
// 	a float value connection in your DG:
// 
//         jitter ("jitX",							// the name of the new
//													// jitter node
// 				"paramCurveTimeToLinear1.output",	// input value
// 				"surface1.translateX"				// output value
// 				);
// 
// 	to jitter the X translation channel of a keyframed 
// 	surface by jittering the function curve
// 	"paramCurveTimeToLinear1.output" and passing the
// 	result to "surface1.translateX".
// 
// 	or:
// 
// 	jitter ("jitX","motionPath1.xCoord", "surface1.translateX");
// 
// 	to jitter the X position of an object "surface1" which 
// 	is animted on a motion path "motionPath1".
// 
// 	Use "setAttr jitX.scale 2;" to set the scale value to 2.

//
//	Script for inserting a jitter node between two float Attributes.
//	If a connection already exists between the Attributes, the procedure
//  will break the connection.
//  If a connection does not already exist between the two Attributes, but
//  the destination Attribute already has an input connection from a different
//  Node, then the procedure fails because fan-in is not allowed in the DG.
//
//	Then setup a slider to control the scale value of the
//	jitter.
//
global proc jitter( string $jitName, string $sourceName, string $destName )
{

	string $jitTime = $jitName + ".time";
	string $jitScale = $jitName + ".scale";
	string $jitInput = $jitName + ".input";
	string $jitOutput = $jitName + ".output";

	//  If the two attributes are connected then disconnect them
	//
	if (`isConnected $sourceName $destName` )
	{
		disconnectAttr  $sourceName  $destName;
	}

	//  Create the Jitter Node
	//
	createNode "jitter" -n $jitName;

	//  Synch the Jitter Node to the Maya Time Slider
	//
	connectAttr  time1.outTime  $jitTime;

	//  "Jitter" to the source value
	//
	connectAttr $sourceName $jitInput;

	//  Pass on the "jittered" value on to the destination
	//
	connectAttr $jitOutput $destName;

	string $winName = $jitName + "_Scale";
	string $layoutName = $jitName + "_Layout";
	string $sliderName = $jitName + "_Slider";

	//  Create Jitter Scale Window
	//
	window -t ($jitName+"Scale Editor") -wh 400 50  $winName;

	// Create UI Controls
	//
	columnLayout -adj true -cal left -cw 400 $layoutName; 
		floatSliderGrp
            -label "Scale"
			-field true
	        -min 0 -max 100 
			-value 1.0
			-changeCommand
                 ("setAttr "+$jitScale+" `floatSliderGrp -q -v "+$sliderName+"`;")
			-dragCommand
                 ("setAttr "+$jitScale+" `floatSliderGrp -q -v "+$sliderName+"`;")
			$sliderName;

	//  Put up the Jitter Scale Window
	//
	showWindow $winName;
}
